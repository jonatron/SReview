#!/usr/bin/perl -w

use strict;
use warnings;

use SReview::Talk;
use SReview::Config::Common;
use YAML::XS;
use JSON;
use DateTime;

my $task = shift;
my $id = shift;

my $json = JSON->new->allow_nonref;

my $config = SReview::Config::Common::setup;

my %task_configs = (
        previews => [ "pubdir", "accessmethods", "s3_access_config",],
        cut => [ "workdir", "pubdir", "audio_multiplex_mode", "accessmethods", "s3_access_config","inputglob",],
        transcode => [ "pubdir", "outputdir", "output_subdirs", "workdir", "preroll_template", "postroll_template", "postroll", "apology_template", "input_profile", "output_profiles", "extra_profiles", "accessmethods", "s3_access_config", ],
        notify => [ "notify_email_template", "notify_email_subject", "notify_commands", "notify_actions", "email_template", "email_subject", "email_from", "urlbase", ],
        announce => [ "announce_email_template", "announce_email_subject", "announce_commands", "announce_actions", "email_template", "email_subject", "email_from", "urlbase", ],
        upload => [ "upload_actions", "event", "pubdir", "inputglob", "outputdir", "output_subdirs", "cleanup", "output_profiles", "extra_profiles", "accessmethods", "s3_access_config", ],
        "inject-job" => [ "input_profile", "pubdir", "inputglob" ],
);

my $talk = SReview::Talk->new(talkid => $id);
my $now = DateTime->now;
my $start = DateTime->new(
	year => $now->year,
	month => $now->month,
	day => 1,
	hour => 0,
	minute => 0,
	second => 0,
);
my $secs = $now->epoch - $start->epoch;
my $jobsuffix = $talk->talkid . "-" . unpack("h*", pack("L", $secs));

my $image = "registry.salsa.debian.org/debconf-video-team/sreview/encoder";
if(exists($ENV{SREVIEW_ENCODER_IMAGE})) {
	$image = $ENV{SREVIEW_ENCODER_IMAGE};
}

my $yamldata = {
        apiVersion => "batch/v1",
        kind => "Job",
        metadata => {name => "$task-$jobsuffix"},
        spec => {
		template => {
			metadata => {
				name => "$task-$jobsuffix",
			},
			spec => {
				restartPolicy => "OnFailure",
				containers => [ {
					name => "encoder",
					image => $image,
					imagePullPolicy => "Always",
					command => ["sreview-$task"],
					args => [ $talk->talkid ],
					env => [ {
						name => "SREVIEW_DBISTRING",
						valueFrom => {
							secretKeyRef => {
								name => "sreview-secret",
								key => "SREVIEW_DBISTRING",
							},
						}
					} ],
				} ],
			},
		},
        },
};

if($task eq "announce") {
        $yamldata->{spec}{template}{spec}{containers}[0]{command}[0] = "sreview-notify";
        push @{$yamldata->{spec}{template}{spec}{containers}[0]{args}}, "announce";
}

foreach my $cfg (@{$task_configs{$task}}) {
	next if $config->is_default($cfg);
	my $val = $json->encode($config->get($cfg));
        push @{$yamldata->{spec}{template}{spec}{containers}[0]{env}}, {name => "SREVIEW_" . uc($cfg), value => $val};
}

if(exists($ENV{DO_DEBUG})) {
	unshift(@{$yamldata->{spec}{template}{spec}{containers}[0]{args}}, ("-d:Trace", "/usr/bin/" . @{$yamldata->{spec}{template}{spec}{containers}[0]{command}}));
	$yamldata->{spec}{template}{spec}{containers}[0]{command} = [ "perl" ];
}

print "About to create this job:\n";
print Dump($yamldata) . "\n";
open my $kubectl, "|-", "kubectl", "apply", "-f", "-";
print $kubectl Dump($yamldata) . "\n";
close $kubectl;
