#!/usr/bin/perl -w

# SReview, a web-based video review and transcoding system
# Copyright (c) 2016-2017, Wouter Verhelst <w@uter.be>
#
# SReview is free software: you can redistribute it and/or modify it
# under the terms of the GNU Affero General Public License as published
# by the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# Affero General Public License for more details.
#
# You should have received a copy of the GNU Affero General Public
# License along with this program.  If not, see
# <http://www.gnu.org/licenses/>.

=head1 NAME

sreview-detect - detect new files for SReview

=head1 SYNOPSIS

sreview-detect

=head1 DESCRIPTION

sreview-detect is used to detect new files in the SReview input
directory, and add them to the database. Additionally, sreview-detect
will update the length (but not the other metadata) of files that
already exist in the database. This makes it safe to run on input files
that are still being written to.

It is designed to be run from cron (or a similar scheduling system) on a
regular basis. Care should be taken to not overload the server on which
it runs; while at the same time it should run regularly enough so that
review is not waiting for too long.

=head1 OPTIONS

None exist currently. Configuration should be done through the main
configuration file; see L<sreview-config>.

=cut

use strict;
use warnings;

use DBI;
use SReview::Config::Common;
use SReview::Video;
use SReview::Files::Factory;

use Net::Domain qw(hostname);

my $config = SReview::Config::Common::setup;

my $dbh = DBI->connect($config->get('dbistring'), '', '') or die "Cannot connect to database!";

$dbh->begin_work;

my $exists = $dbh->prepare("SELECT mtime FROM raw_files WHERE filename = ?");
my $add = $dbh->prepare("INSERT INTO raw_files(filename, room, starttime, endtime, stream, mtime) VALUES (?, (SELECT id FROM rooms WHERE altname = ? OR name = ?), ?::timestamptz, ?::timestamptz + ?::interval, ?, ?)");
my $update = $dbh->prepare("UPDATE raw_files SET endtime = starttime + ?::interval, mtime = ? WHERE filename = ?");

my $verbose = 0;
$verbose = 1 if exists($ENV{KUBERNETES_PORT});

my $collection = SReview::Files::Factory->create("input", $config->get("inputglob"));

print "searching collection at " . $collection->baseurl . ", accessing for files using " . $collection->fileclass . "\n" if $verbose;

my $host = hostname();


foreach my $file(@{$collection->children}) {
	next if ($file->is_collection);
	print "\nfound " . $file->url if $verbose;
	my $parse_re = $config->get('parse_re');
	next unless $file->url =~ /$parse_re/;
	print " ...passes regex" if $verbose;
	my $room = $+{room};
	my $start_hour = $+{hour} . ":" . $+{minute} . ":" . $+{second};
	my $start_day = $+{year} . "-" . $+{month} . "-" . $+{day};
	my $stream = $+{stream};
	$stream = '' unless defined $stream;
	next unless defined($room);
	$exists->execute($file->url);
	my $row = $exists->fetchrow_hashref;
	if($exists->rows == 0) {
		print " ...is new" if $verbose;
		my $video = SReview::Video->new(url => $file->filename);
		my $length = $video->duration;
		next unless defined($length);
		$add->execute($file->url, $room, $room, "$start_day $start_hour", "$start_day $start_hour", $length, $stream, $file->mtime->epoch);
		print "... stored" if $verbose;
	} else {
		print " ... is known" if $verbose;
		next unless ($row->{mtime} != $file->mtime->epoch);
		print " ... but has changed" if $verbose;
		my $video = SReview::Video->new(url => $file->filename);
		my $length = $video->duration;
		next unless defined($length);
		$update->execute("$length", $file->mtime->epoch, $file->url);
		print " ... length stored" if $verbose
	}
}

print "\ndone\n" if $verbose;

my $full = $dbh->prepare("UPDATE talks SET progress = 'done' WHERE state = 'waiting_for_files' AND progress < 'done' AND id IN (select talkid FROM raw_talks WHERE talks_length <= (raw_total + '5 seconds'::interval)) AND endtime < now() - '20 minutes'::interval");
$full->execute();
my $partial = $dbh->prepare("UPDATE talks SET progress = 'running' WHERE state = 'waiting_for_files' AND progress < 'running' AND id IN (select distinct talkid FROM raw_talks)");
$partial->execute();

$dbh->commit;
